open Lexer
open Utils
open Syntax

let debug = false

(** Result of attempt to parse a global declaration *)
type parsed_let_expression =
  | DeclToBe of int * global_declaration * token list
  | LetInToBe of int * (variable * expr) * token list
  | Nothing

(** The content of what can be parsed between ML brackets *)
type ml_code = Global of global_declaration list | Expr of expr

(** Eating functions. When expecting a certain token, use it to get: the new line number, the important data of the token read, and the remainder of the token list.
  Must be provided with an error message to display in case the next token is not the token expected *)

let eat_variable (l : token list) (error_msg : string) : int * variable * token list = match l with
  | (i_line, Id name) :: l_rem -> (i_line, name, l_rem)
  | _ -> raise (ParsingError error_msg)

let eat_html (l : token list) (error_msg : string) : int * string * token list = match l with
  | (i_line, TokHtml code) :: l_rem -> (i_line, code, l_rem)
  | _ -> raise (ParsingError error_msg)

let eat_token (tok : raw_token) (l : token list) (error_msg : raw_token option -> string) : int * raw_token * token list = match l with
  | (i_line, tok') :: l_rem -> if tok = tok' then (i_line, tok, l_rem) else raise (ParsingError (error_msg (Some tok')))
  | _ -> raise (ParsingError (error_msg None))

(** Eats a token among [toks] if it is the next to be read; [None] otherwise *)
let eat_token_opt (toks : raw_token list) (l : token list) : (int * raw_token * token list) option = match l with
  | (i_line, tok') :: l_rem -> if List.mem tok' toks then Some (i_line, tok', l_rem) else None
  | _ -> None

(** TYPE EXPRESSIONS *)

let parse_type_identifier (l : token list) : int * type_name * token list = eat_variable l "type name expected."

let parse_type_param (l : token list) : int * variable * token list = 
  let _, _, l_rem = eat_token (Keyword TokLpar) l (fun t -> match t with | None -> "quote expected." | Some tok -> Printf.sprintf "%s: quote expected." (string_of_raw_token tok)) in
  parse_type_identifier l_rem

(** [parse_type_params l = (i, [var1, ..., varn], l_rem)] parses type parameters [('var1, ..., 'varn)] from the beginning of [l]. Can be 'a or ('a1, ..., 'an). *)
let parse_type_params (l : token list) : int * variable list * token list =
  (** parses ['var1, 'var2, ..., 'varn)]*)
  let rec parse_type_params_until_close_par (l : token list) (acc : variable list) : int * variable list * token list = match eat_token_opt [Keyword TokRpar] l with
    | Some (ipar, rpar, l_rem) -> ipar, (List.rev acc), l_rem
    | None -> begin let i, var_name, l_rem = parse_type_param l in
      let i_comma, comma, l_rem = eat_token (Keyword TokComma) l_rem (fun t -> match t with | None -> "" | Some tok -> Printf.sprintf "%s: comma expected." (string_of_raw_token tok)) in
      parse_type_params_until_close_par l_rem (var_name :: acc)
    end
  in match eat_token_opt [Keyword TokLpar] l with
    | Some (ipar, lpar, l_rem) -> parse_type_params_until_close_par l_rem []
    | None -> let i, var_name, l_rem = parse_type_param l in (i, [var_name], l_rem)

(** [parse_type_record_opt l] tries to parse a record type expression. If doesn't start with a `{`, returns [None] *)
let parse_type_record_opt (l : token list) : (int * expr * token list) option = match eat_token_opt [Keyword TokRBracket] l with
  | Some (i_line, open_curly_bracket, l_rem) -> failwith "TODO: parse record types."
  | _ -> None


let parse_type_expr (l : token list) : int * expr * token list = failwith "TODO"

let parse_type_constrargs (l : token list) : int * expr * token list = failwith "TODO"

let parse_type_constrdecl (l : token list) : int * expr * token list =
  let i, constr_name, lrem = eat_token (MId "") l (fun t -> match t with | None -> "constructor name expected." | Some tok -> Printf.sprintf "%s: constructor name expected." (string_of_raw_token tok)) in
  match constr_name with
    | MId constrname -> begin match eat_token_opt [Keyword TokOf] lrem with
      | Some (i, tok_of, lrem) -> failwith "TODO"
      | None -> failwith "TODO"
    end
    | _ -> assert false

let parse_type_sum (l : token list) : int * expr * token list = failwith "TODO"

(** LEFT ASSOCIATIVITY *)

let merge_additive (op : raw_token) (e1 : expr) (e2 : expr) = match op with
    | Keyword TokPlus -> Plus (e1, e2)
    | Keyword TokMinus -> Minus (e1, e2)
    | _ -> assert false

let merge_multiplicative (op : raw_token) (e1 : expr) (e2 : expr) = match op with
    | Keyword TokTimes -> Mult (e1, e2)
    | Keyword TokDiv -> Div (e1, e2)
    | _ -> assert false

(** [parse_left_assoc parse_operand sep merge_operands last_line acc l] parses from [l] where the last line number from which we read was [last_line] given an accumulator of expressions already parsed [acc].
  The goal is to parse a series of expressions parsed by [parse_operand] separated by separator in the list [sep].
  They are combined in a left-associative manner using the function [merge_operands].
  Example:
  [parse_left_assoc parse_factors [KeyWord TokPlus; KeyWord TokMinus] merge_additive 156 (Int 0) [Keyword TokPlus; Lit TokInt 3; Keyword TokTimes; Lit TokInt 2; Keyword TokMinus; Lit TokInt 4]] parses [((0 + (3 * 2)) - 4)].
*)
let rec parse_left_assoc (parse_operand : token list -> (int * expr * token list)) (sep : raw_token list) (merge_operands : raw_token -> expr -> expr -> expr) (last_line : int) (acc : expr) (l : token list) : int * expr * token list =
  match eat_token_opt sep l with
  | Some (i_line, operator, l_rem) -> let i_line', operand, l_rem' = parse_operand l_rem in
    parse_left_assoc parse_operand sep merge_operands i_line' (merge_operands operator acc operand) l_rem'
  | _ -> (last_line, acc, l)

let rec parse_application_accumulator (parse_argument : token list -> (int * expr * token list)) (last_line : int) (acc : expr) (l : token list) : int * expr * token list =
  try
    let i_line, arg, l_rem = parse_argument l in
    parse_application_accumulator parse_argument i_line (App (acc, arg)) l_rem (* can't raise a ParsingError *)
  with
    | ParsingError _ -> (last_line, acc, l)

(** Parse functions' parameter *)

let eat_parameter (i : int) (l : token list) (error_message : int -> string) : int * parameter * token list =
  eat_variable l (error_message i)

(** [eat_parameters i l = (i, [p1;...;pn], l_rem)] parses all function parameters [[p1;...;pn]] from [l] until it founds a [->]. [lrem] is the tail [l], from just after the [->] token. *)
let eat_parameters (i : int) (l : token list) (error_message : int -> string) : int * parameter list * token list =
  let rec eat_parameters_acc (i : int) (l : token list) (acc : parameter list) : int * parameter list * token list =
    match eat_token_opt [Keyword TokArr] l with
    | Some (i_arr, tok_arr, l_rem) -> i_arr, List.rev acc, l_rem
    | None -> (* it must be a parameter *) let i_p, p, l_rem = eat_parameter i l error_message in eat_parameters_acc i_p l_rem (p :: acc)
  in
  eat_parameters_acc i l []

(** Returns [(last_line, remaining, e)]: [last_line] the number of the last line from which we parsed something; [remaining] the list of tokens that remained to be parsed; [e] the parsed expression *)
let rec parse_exp (l : token list) : int * expr * token list = if debug then Printf.fprintf stderr "PARSING: %s\n" (string_of_list string_of_token l); parse_sequence l
and parse_sequence (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE SEQ\n%!");
  let i_lhs, lhs, l_rem = parse_if l in
  (if debug then Printf.fprintf stderr "PARSE SEQ OK\n%!");
  match eat_token_opt [(Keyword TokSeq)] l_rem with
    | Some (i_seq, seq, l_rem) ->
      let i_rhs, rhs, l_rem = parse_sequence l_rem in (i_rhs, Seq (lhs, rhs), l_rem)
    | None -> (i_lhs, lhs, l_rem)
and parse_if (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE IF\n%!");
  match l with
  | [] -> raise (ParsingError "Unexpected end of document")
  | (i, tok) :: l_rem -> begin match tok with
    | Keyword TokIf -> begin
      let i_condition, condition, l_rem = parse_if l_rem in
      let i_then, tok_then, l_rem = eat_token (Keyword TokThen) l_rem (fun _ -> Printf.sprintf "line %d: if-expression: `then` expected after 'if <condition>'." i_condition) in
      let i_then_body, then_body, l_rem = parse_if l_rem in
      begin match eat_token_opt [Keyword TokElse] l_rem with
        | None -> (i_then_body, If (condition, then_body, Unit), l_rem)
        | Some (i_else, tok_else, l_rem) -> let i_else_body, else_body, l_rem = parse_if l_rem in
          (i_else_body, If (condition, then_body, else_body), l_rem)
      end
    end
    | _ -> parse_tuple l
  end
and parse_tuple (l : token list) : int * expr * token list = (* for now, only couples are allowed *)
(if debug then Printf.fprintf stderr "PARSE TUPLE\n%!");
  let i_lhs, lhs, l_rem = parse_disjunction l in
  match eat_token_opt [(Keyword TokComma)] l_rem with
    | Some (i_comma, op_comma, l_rem) ->
      let i_rhs, rhs, l_rem = parse_tuple l_rem in (i_rhs, Couple (lhs, rhs), l_rem)
    | None -> (i_lhs, lhs, l_rem)
and parse_disjunction (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE DISJUNCTION\n%!");
  let i_lhs, lhs, l_rem = parse_conjunction l in
  match eat_token_opt [(Keyword TokOr)] l_rem with
    | Some (i_or, op_or, l_rem) ->
      let i_rhs, rhs, l_rem = parse_disjunction l_rem in (i_rhs, Or (lhs, rhs), l_rem)
    | None -> (i_lhs, lhs, l_rem)
and parse_conjunction (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE CONJUNCTION\n%!");
  let i_lhs, lhs, l_rem = parse_comparison l in
  match eat_token_opt [(Keyword TokAnd)] l_rem with
    | Some (i_and, op_and, l_rem) ->
      let i_rhs, rhs, l_rem = parse_conjunction l_rem in (i_rhs, And (lhs, rhs), l_rem)
    | None -> (i_lhs, lhs, l_rem)
and parse_comparison (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE COMPARISON\n%!");
  let i_lhs, lhs, l_rem = parse_concatenation l in
  match eat_token_opt [Keyword TokEq; Keyword TokNeq; Keyword TokGt; Keyword TokLt; Keyword TokGeq; Keyword TokLeq] l_rem with
    | Some (i_op, Keyword TokEq, l_rem) ->
      let i_rhs, rhs, l_rem = parse_comparison l_rem in (i_rhs, Eq (lhs, rhs), l_rem)
    | Some (i_op, Keyword TokNeq, l_rem) ->
      let i_rhs, rhs, l_rem = parse_comparison l_rem in (i_rhs, Neq (lhs, rhs), l_rem)
    | Some (i_op, Keyword TokGt, l_rem) ->
      let i_rhs, rhs, l_rem = parse_comparison l_rem in (i_rhs, Gt (lhs, rhs), l_rem)
    | Some (i_op, Keyword TokLt, l_rem) ->
      let i_rhs, rhs, l_rem = parse_comparison l_rem in (i_rhs, Lt (lhs, rhs), l_rem)
    | Some (i_op, Keyword TokGeq, l_rem) ->
      let i_rhs, rhs, l_rem = parse_comparison l_rem in (i_rhs, Geq (lhs, rhs), l_rem)
    | Some (i_op, Keyword TokLeq, l_rem) ->
      let i_rhs, rhs, l_rem = parse_comparison l_rem in (i_rhs, Leq (lhs, rhs), l_rem)
    | None -> (i_lhs, lhs, l_rem)
    | _ -> assert false
and parse_concatenation (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE CONCATENATION\n%!");
  let i_lhs, lhs, l_rem = parse_sum l in
  match eat_token_opt [(Keyword TokStrConcat)] l_rem with
    | Some (i_concat, concat, l_rem) ->
      let i_rhs, rhs, l_rem = parse_concatenation l_rem in (i_rhs, Concat (lhs, rhs), l_rem)
    | None -> (i_lhs, lhs, l_rem)
and parse_sum (l : token list) : int * expr * token list =
  let i, first_operand, l_rem = parse_multiplication l in
  parse_left_assoc parse_multiplication [Keyword TokPlus; Keyword TokMinus] merge_additive i first_operand l_rem
and parse_multiplication (l : token list) : int * expr * token list =
  let i, first_operand, l_rem = parse_power l in
  parse_left_assoc parse_power [Keyword TokTimes; Keyword TokDiv] merge_multiplicative i first_operand l_rem
and parse_power (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE POWER\n%!");
  let i_number, number, l_rem = parse_negation l in
  match eat_token_opt [(Keyword TokExp)] l_rem with
    | Some (i_pow_op, pow_op, l_rem) ->
      let i_power, power, l_rem = parse_power l_rem in (i_power, Pow (number, power), l_rem)
    | None -> (i_number, number, l_rem)
and parse_negation (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE NEGATION\n%!");
match l with
  | (i, Keyword TokMinus) :: l_rem ->
    let i_number, number, l_rem = parse_application l_rem in
    (i_number, Neg number, l_rem)
  | _ -> parse_application l 
and parse_application (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE APPLICATION\n%!");
  let i_func, func, l_rem = parse_atom l in
  parse_application_accumulator parse_atom i_func func l_rem
and parse_atom (l : token list) : int * expr * token list =
  (if debug then Printf.fprintf stderr "PARSE ATOM\n%!");
  match l with
  | [] -> raise (ParsingError "Unexpected end of document")
  | (i, tok) :: l_rem -> begin match tok with
    (* Constructors for which we know how to parse by reading the first token *)
    | Keyword TokLet -> begin
      let i_var, var, l_rem = eat_variable l_rem (Printf.sprintf "line %d: let-expression: variable expected after 'let'." i) in
      let i_eq, eq, l_rem = eat_token (Keyword TokEq) l_rem (fun _ -> Printf.sprintf "line %d: let-expression: '=' expected after 'let'." i_var) in
      let i_x_expr, x_expr, l_rem = parse_exp l_rem in
      let i_in, in_, l_rem = eat_token (Keyword TokIn) l_rem (fun _ -> Printf.sprintf "line %d: let-expression: 'in' expected after 'let'." i_x_expr) in
      let i, body_expr, l_rem = parse_exp l_rem in
      (i, Let (var, x_expr, body_expr), l_rem)
    end
    | Keyword TokFun -> begin
      let i_params, params, l_rem = eat_parameters i l_rem (Printf.sprintf "line %d: fun-expression: parameters expected after 'fun'.") in
      let i_body, body_e, l_rem = parse_exp l_rem in
      (i_body, List.fold_right (fun p body -> Fun (p, body)) params body_e, l_rem)
    end
    | Keyword TokFix -> begin
      let i_f_var, f_var, l_rem = eat_variable l_rem (Printf.sprintf "line %d: fixfun-expression: function variable expected after 'fixfun'." i) in
      let i_params, params, l_rem = eat_parameters i l_rem (Printf.sprintf "line %d: fixfun-expression: parameters expected after 'fixfun'.") in
      let first_param = List.hd params in (* there has to be at least one parameter *)
      let other_params = List.tl params in
      let i_body_e, body_e, l_rem = parse_exp l_rem in
      (* For [fixfun f x1 x2 ... xn -> e], [actual_body] represents [fun x2 ... xn -> e], since it is seen as [fixfun f x1 -> fun x2 ... xn -> e] *)
      let actual_body = List.fold_right (fun p body -> Fun (p, body)) other_params body_e in
      (i_body_e, Fix (f_var, first_param, actual_body), l_rem)
    end
    | Keyword TokCloseML -> raise (ParsingError (Printf.sprintf "line %d: Unexpected end of ml code." i))
    | Lit TokTrue -> (i, Bool true, l_rem)
    | Lit TokFalse -> (i, Bool false, l_rem)
    | Lit (TokInt n) -> (i, Int n, l_rem)
    | Lit (TokStr s) -> (i, String s, l_rem)
    (* | Lit (TokFstr s) -> (i, Fstring s, l_rem) *)
    | Id s -> (i, Var s, l_rem)
    | Keyword TokLpar ->
      begin match eat_token_opt [Keyword TokRpar] l_rem with
        | None -> let i_expr, expr, l_rem = parse_exp l_rem in
          let i_rpar, rpar, l_rem = eat_token (Keyword TokRpar) l_rem (fun x ->
            Printf.sprintf "line %d: %s closing parenthesis expected." i_expr
              (match x with
                | Some tok -> string_of_raw_token tok
                | None -> ""
              )
            ) in
          (i_rpar, expr, l_rem)
        | Some (i_rpar, rpar, l_rem) -> (i_rpar, Unit, l_rem)
    end
    | Keyword TokOpenHTML ->
      let line_after_close_html, html_sub, l_rem' = parse_html_unit l_rem false in (line_after_close_html, Html html_sub, l_rem')
    | TokHtml s -> raise (ParsingError (Printf.sprintf "line %d: Unexpected html code within ML delimiters." i)) (* assert false ? *)
    | MId modu ->
      let i_dot, dot, l_rem = eat_token (Keyword TokDot) l_rem (fun x -> Printf.sprintf "line %d: expected dot after '%s'." i modu) in (* TODO at some point, this error will not appear, since it'll be interpreted as a constructor *)
      let i_scope_of_module, expr_in_scope_of_module, l_rem = parse_atom l_rem in (* TODO too permissive e.g. ModuleName.fun x -> x shouldn't be parsed as ModuleName.(fun x -> x). Investigate. *)
      (i_scope_of_module, WithModule (modu, expr_in_scope_of_module), l_rem)
    | Keyword TokOpenFstring -> parse_fstring l_rem
    | _ -> raise (ParsingError (Printf.sprintf "line %d: Malformed expression." i))
  end

(** PARSING GLOBALS *)

(** [parse_global l] tries to parse a global from the token list [l], in the form of a [parsed_let_expression]; it can either be:
  - [DeclToBe] : parsed a global declaration;
  - [LetInToBe] : we started to parse a let ... = ... in, but, as a [in] follows, we return the variable name and the expression, and the remaining list of token starts after this [in];
  - [Nothing] : something else needs to be parsed. *)
and parse_global (l : token list) : parsed_let_expression =
  (* We start by testing whether the code starts with a `ModuleName.` and if so, we store the module name. For now, only supports one-level module e.g. `ModuleName1.ModuleName2.let x = 5` can't be a valid global declaration *)
  let l', module_declaration = match eat_token_opt [MId session_module_name] l with
    | Some (i_session, MId module_name, l_rem) when module_name = session_module_name ->
    begin match eat_token_opt [Keyword TokDot] l_rem with
      | Some (i_dot, dot, l_rem') -> l_rem', (Some module_name)
      | None -> [], None
    end
    | None | Some _ -> l, None
  in
  match eat_token_opt [Keyword TokLet; Keyword TokType; Keyword TokOpen; Keyword TokImport] l' with
    | Some (i_let, Keyword TokLet, l_rem) -> begin
      let i_var, var, l_rem = eat_variable l_rem (Printf.sprintf "line %d: let-expression: variable expected after 'let'." i_let) in
      let i_eq, eq, l_rem = eat_token (Keyword TokEq) l_rem (fun _ -> Printf.sprintf "line %d: let-expression: '=' expected after 'let'." i_var) in
      let i_x_expr, x_expr, l_rem = parse_exp l_rem in
      begin match eat_token_opt [(Keyword TokIn)] l_rem with
        | Some (_, tok_in, l_rem') -> LetInToBe (i_x_expr, (var, x_expr), l_rem')
        | None -> (* we parsed a global let declaration. we now test if it's a module global or a vanilla global *)
          begin match module_declaration with
            | None -> DeclToBe (i_x_expr, ExprDecl (var, x_expr), l_rem)
            | Some mod_name -> DeclToBe (i_x_expr, ModuleExprDecl (mod_name, var, x_expr), l_rem)
          end
      end 
    end
    | Some (i_let, Keyword TokImport, l_rem) -> begin match l_rem with
      | (line, Lit (TokStr path)) :: l_rem -> DeclToBe (line, ImportModule path, l_rem)
      | [] -> raise (ParsingError "Unexpected end of document.")
      | _ :: _ -> raise (ParsingError "Path to .tml file expected after `import`")
    end
    | Some (i_let, Keyword TokOpen, l_rem) -> begin match l_rem with (* TODO generalize to hoisting subsubsub...submodule (at any depth), cf TODO in hierarchic *)
      | (line, MId module_name) :: l_rem -> DeclToBe (line, OpenModule module_name, l_rem)
      | [] -> raise (ParsingError "Unexpected end of document.")
      | _ :: _ -> raise (ParsingError "Module expected after `open`")
    end
    | Some (i_let, Keyword TokType, l_rem) -> begin
      failwith "TODO"
    end
    | None -> Nothing
    | _ -> assert false

(** [parse_fstring l] parses the content of a fstring (except the opening f+quote )*)
and parse_fstring (l : token list) : int * expr * token list =
  let rec parse_fstring_acc (l : token list) (acc : fstr_element list) : int * expr * token list = match l with
    | (i, Keyword TokCloseFstring) :: lrem -> i, Fstring (List.rev acc), lrem
    | (i, Lit (TokFstr s)) :: lrem -> parse_fstring_acc lrem (FstrString s :: acc)
    | (i, Keyword TokOpenFexpr) :: lrem -> let i_exp, e, lrem = parse_exp lrem in begin match lrem with
      | (_, Keyword TokCloseFexpr) :: lrem -> parse_fstring_acc lrem (FstrExpr e :: acc)
      | (i, tok) :: lrem -> raise (ParsingError (Printf.sprintf "line %d: %s, string or fstring open-ml bracket expected." i (string_of_raw_token tok)))
      | [] -> raise (ParsingError "Malformed expression: unclosed fstring expression (unexpected end of document).")
    end
    | (i, tok) :: _ -> raise (ParsingError (Printf.sprintf "line %d: %s, string or fstring open-ml bracket expected." i (string_of_raw_token tok)))
    | [] -> raise (ParsingError "Malformed expression: unclosed fstring (unexpected end of document).")
  in
  parse_fstring_acc l []

(** [parse_globals l acc] parses a sequence of global declaration from [l], appended to [acc] *)
and parse_globals (l : token list) (acc : global_declaration list) : int * global_declaration list * token list = match l with
  | [] -> raise (ParsingError "Unexpected end of document")
  | (i, Keyword TokCloseML) :: l_rem -> (i, acc, (i, Keyword TokCloseML) :: l_rem) (* TODO not exactly (for the line number) should be the line number of previous token *)
  | (i, h_tok) :: l_rem -> begin match parse_global ((i, h_tok) :: l_rem) with
    | DeclToBe (i, g, l_rem) -> parse_globals l_rem (g :: acc)
    | _ -> raise (ParsingError (Printf.sprintf "line %d: Unexpected expression after global declaration" i))
  end

and parse_ml_code (l : token list) : int * ml_code * token list =
  match parse_global l with
  | DeclToBe (i, g, l_rem) -> let i, parsed_globals, l_rem = parse_globals l_rem [g] in (i, Global (List.rev parsed_globals), l_rem)
  | Nothing -> let i, e, l_rem' = parse_exp l in (i, Expr e, l_rem')
  | LetInToBe (i_line, (x, e), l_rem) -> let i, body, l_rem = parse_exp l in (i, Expr (Let (x, e, body)), l_rem)

(** [parse_html_unit lexed is_root] parses an HTML unit i.e. either the whole [lexed] list or html content delimited by html-opening/closing brackets, depending on [is_root].
  If [is_root] is false, then a CloseHtml token stops the parsing. Otherwise, stops parsing only at the end of the [lexed] list. *)
and parse_html_unit (lexed : token list) (is_root : bool) : int * dynml_webpage * token list = match lexed with
  | [] -> (-1, [], [])
  | [(i, TokHtml s)] -> (i, [Pure s], [])
  | (i, TokHtml s) :: (j, Keyword TokCloseHTML) :: l_rem -> (j, [Pure s], l_rem)
  | (i, TokHtml s) :: (j, Keyword TokOpenML) :: lexed' -> begin match parse_ml_code lexed' with
    | i_line, Expr parsed, (_, Keyword TokCloseML) :: l_rem' -> let final_line, final_parsed, final_l_rem = parse_html_unit l_rem' is_root in (final_line, (Pure s) :: (Script parsed) :: final_parsed, final_l_rem)
    | i_line, Global globals, (_, Keyword TokCloseML) :: l_rem' ->
      let final_line, final_parsed, final_lrem = parse_html_unit l_rem' is_root in
      (final_line, (Pure s) :: (List.map (fun x -> Decl x) globals) @ final_parsed, final_lrem)
    | i_line, _, tok :: _ -> raise (ParsingError (Printf.sprintf "line %d: %s, ML-closing bracket %s expected" i_line (string_of_token tok) (string_of_raw_token (Keyword TokCloseML))))
    | i_line, _, [] -> raise (ParsingError (Printf.sprintf "line %d: ML-closing bracket %s expected" i_line (string_of_raw_token (Keyword TokCloseML))))
  end
  | _ -> Printf.fprintf stderr "%s\n" (string_of_list string_of_token lexed); raise (ParsingError "Unexpected error")

(** Parsing ml *)
let rec parser (lexed : token list) : dynml_webpage = let _, parsed, _ = parse_html_unit lexed true in parsed